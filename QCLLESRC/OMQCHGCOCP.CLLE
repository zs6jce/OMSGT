 /* ---------------------------------------------------------------- */
 /*                                                                  */
 /*  Program ....: OMQCHGCOCP                                        */
 /*  Description : CPP FOR COMMAND OMQCHGCO - Contact API            */
 /*  Author .....: Remain Software                                    */
 /*  Date .......: 10 07 2000                                        */
 /*                                                                  */
 /*                                                                  */
 /* ---------------------------------------------------------------- */

 /* ---------------------------------------------------------------- */
 /* Program                                                          */
 /* ---------------------------------------------------------------- */
             PGM        PARM(&ACTC &CUSC &CCTC &TITL &NAMD &TELN +
                          &CNTI &EMAC &MTDI &USF1 &USF2 &USF3 &NCCT +
                          &EOPI)

          /* ------------------------------------------------------- */
          /* Declare parameter-variables                             */
          /* ------------------------------------------------------- */
             DCL        VAR(&ACTC) TYPE(*CHAR) LEN(002)
             DCL        VAR(&CUSC) TYPE(*CHAR) LEN(010)
             DCL        VAR(&CCTC) TYPE(*CHAR) LEN(010)
             DCL        VAR(&TITL) TYPE(*CHAR) LEN(030)
             DCL        VAR(&NAMD) TYPE(*CHAR) LEN(050)
             DCL        VAR(&TELN) TYPE(*CHAR) LEN(015)
             DCL        VAR(&CNTI) TYPE(*CHAR) LEN(001)
             DCL        VAR(&EMAC) TYPE(*CHAR) LEN(050)
             DCL        VAR(&MTDI) TYPE(*CHAR) LEN(001)
             DCL        VAR(&USF1) TYPE(*CHAR) LEN(010)
             DCL        VAR(&USF2) TYPE(*CHAR) LEN(025)
             DCL        VAR(&USF3) TYPE(*CHAR) LEN(008)
             DCL        VAR(&NCCT) TYPE(*CHAR) LEN(010)
             DCL        VAR(&EOPI) TYPE(*CHAR) LEN(1)
             DCL        VAR(&UPDI) TYPE(*CHAR) LEN(1) VALUE('1')
             DCL        VAR(&UPBI) TYPE(*CHAR) LEN(1) VALUE('1')

          /* ------------------------------------------------------- */
          /* Declare program variables                               */
          /* ------------------------------------------------------- */
             DCL        VAR(&RES#) TYPE(*CHAR) LEN(002)
             DCL        VAR(&INTL) TYPE(*CHAR) LEN(8)
             DCL        VAR(&BUF)  TYPE(*CHAR) LEN(1024)
             DCL        VAR(&PRC)  TYPE(*CHAR) LEN(40)
             DCL        VAR(&ERR)  TYPE(*CHAR) LEN(136)
             DCL        VAR(&FLD)  TYPE(*CHAR) LEN(130)

             DCL        VAR(&USF31) TYPE(*CHAR) LEN(8) +
                          VALUE(X'000000000000001D')

          /* ------------------------------------------------------- */
          /* Monitor                                                 */
          /* ------------------------------------------------------- */
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))

 /* ---------------------------------------------------------------- */
 /* Processing starts here                                           */
 /* ---------------------------------------------------------------- */

          /* ------------------------------------------------------- */
          /* Close program when ACTC = 99                            */
          /* ------------------------------------------------------- */
             IF         COND(&ACTC *EQ '99') THEN(DO)
             CALL       PGM(OMQCHGCO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

          /* ------------------------------------------------------- */
          /* Change input buffer for special values                  */
          /* ------------------------------------------------------- */
             IF         COND(&USF3 *EQ &USF31) THEN(CHGVAR +
                          VAR(&USF3) VALUE('*SAME   '))

/* ----------------------------------------------------------------- */
/* Initialize buffer                                                 */
/* ----------------------------------------------------------------- */
             CHGVAR     VAR(&INTL) VALUE('V3R0M0  ')
             CHGVAR     VAR(&BUF)  VALUE(' ')
             CHGVAR     VAR(&PRC)  VALUE(' ')
             CHGVAR     VAR(&ERR)  VALUE(' ')
             CHGVAR     VAR(&FLD)  VALUE(' ')

/* ----------------------------------------------------------------- */
/* Fill Processing indicators                                        */
/* ----------------------------------------------------------------- */
             CHGVAR     VAR(%SST(&PRC 1 1))   VALUE(&EOPI)
             CHGVAR     VAR(%SST(&PRC 2 1))   VALUE(&UPDI)
             CHGVAR     VAR(%SST(&PRC 3 1))   VALUE(&UPBI)

/* ----------------------------------------------------------------- */
/* Fill Buffer data                                                  */
/* ----------------------------------------------------------------- */
             CHGVAR     VAR(     &BUF         ) VALUE('   ')
             CHGVAR     VAR(%SST(&BUF   1  10)) VALUE(&CUSC)
             CHGVAR     VAR(%SST(&BUF  11  10)) VALUE(&CCTC)
             CHGVAR     VAR(%SST(&BUF  21  01)) VALUE(&CNTI)
             CHGVAR     VAR(%SST(&BUF  22  30)) VALUE(&TITL)
             CHGVAR     VAR(%SST(&BUF  52  50)) VALUE(&NAMD)
             CHGVAR     VAR(%SST(&BUF 102  15)) VALUE(&TELN)
             CHGVAR     VAR(%SST(&BUF 117  50)) VALUE(&EMAC)
             CHGVAR     VAR(%SST(&BUF 167  05)) VALUE(X'000000000D')
             CHGVAR     VAR(%SST(&BUF 170  10)) VALUE(&USF1)
             CHGVAR     VAR(%SST(&BUF 180  25)) VALUE(&USF2)
             CHGVAR     VAR(%SST(&BUF 205  08)) VALUE(&USF3)
             CHGVAR     VAR(%SST(&BUF 213  10)) VALUE(&NCCT)
             CHGVAR     VAR(%SST(&BUF 223  01)) VALUE(&MTDI)

/* ----------------------------------------------------------------- */
/* Call OMS API OMQCHGCO - Change Customer Contact                   */
/* ----------------------------------------------------------------- */
             CALL       PGM(OMQCHGCO) PARM(&INTL &ACTC &BUF &PRC +
                          &ERR &FLD)

/* ----------------------------------------------------------------- */
/* Send completion message when no errors                            */
/* ----------------------------------------------------------------- */
             IF         COND(%SST(&ERR 8 1) *EQ ' ') THEN(DO)
             SNDPGMMSG  MSGID(OMQA703) MSGF(OMSAPI) MSGDTA(%SST(&BUF +
                          1 10)) MSGTYPE(*COMP)
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/* ----------------------------------------------------------------- */
/* When messages have been send, send them to the caller             */
/* ----------------------------------------------------------------- */
             IF         COND(%SST(&ERR 1 7) *EQ ' ') THEN(DO)

             CALL       PGM(QMHMOVPM) PARM('    ' '*COMP     +
                          *DIAG     *ESCAPE   *INFO     ' +
                          X'00000004' 'OMQCHGCOCP' X'00000001' +
                          X'00000000')
             MONMSG     MSGID(CPF0000)

             ENDDO

/* ----------------------------------------------------------------- */
/* Send message when error or warning                                */
/* ----------------------------------------------------------------- */
             IF         COND(%SST(&ERR 1 7) *EQ ' ') THEN(SNDPGMMSG +
                          MSGID(OMQ5020) MSGF(OMSAPI) +
                          MSGDTA(OMQCHGCO) MSGTYPE(*ESCAPE))
             ELSE       CMD(SNDPGMMSG MSGID(%SST(&ERR 1 7)) +
                          MSGF(OMSAPI) MSGDTA(%SST(&ERR 9 128)) +
                          MSGTYPE(*ESCAPE))

/* ----------------------------------------------------------------- */
/* Go to end of program                                              */
/* ----------------------------------------------------------------- */
             GOTO       CMDLBL(ENDPGM)

/* ----------------------------------------------------------------- */
/* An error occured                                                  */
/* ----------------------------------------------------------------- */
 ERROR:
             SNDPGMMSG  MSGID(OMQ5012) MSGF(OMSAPI) +
                          MSGDTA('OMQCHGCO') MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)
             RETURN

/* ----------------------------------------------------------------- */
/* End of Program                                                    */
/* ----------------------------------------------------------------- */
 ENDPGM:
             RETURN
             ENDPGM
