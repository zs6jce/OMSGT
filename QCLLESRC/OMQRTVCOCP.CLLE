 /* ---------------------------------------------------------------- */
 /*                                                                  */
 /*  Program ....: OMQRTVCOCP                                        */
 /*  Description : CPP for Retrieve Customer Contact                 */
 /*  Author .....: Remain Software                                    */
 /*  Module .....: Kernel                                            */
 /*  Date .......: 25 02 1997                                        */
 /*                                                                  */
 /*  Input ......: &CUSC       - Customer                            */
 /*                &CCTC       - Contact                             */
 /*                                                                  */
 /*  Output .....: &CUSC       - Customer                            */
 /*                &CCTC       - Contact                             */
 /*                &TITL       - Titel                               */
 /*                &NAMD       - Name                                */
 /*                &TEL#       - Telephone                           */
 /*                &EMAC       - Email                               */
 /*                &USF1       - User field 1                        */
 /*                &USF2       - User field 2                        */
 /*                &USF3       - User field 3                        */
 /*                                                                  */
 /*  Description : This program is the command processor for         */
 /*  program OMQRTVCO. This command allows the user to retrieve      */
 /*  CCO attributes from a CCO in the CCO file.                      */
 /*                                                                  */
 /* ---------------------------------------------------------------- */
             PGM        PARM(&CUSC &CCTC &TITL &NAMD &TEL# &EMAC +
                          &USF1 &USF2 &USF3)

 /* ---------------------------------------------------------------- */
 /* Declare the varaibles                                            */
 /* ---------------------------------------------------------------- */
             DCL        VAR(&CUSC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CCTC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TITL) TYPE(*CHAR) LEN(30)
             DCL        VAR(&NAMD) TYPE(*CHAR) LEN(50)
             DCL        VAR(&TEL#) TYPE(*CHAR) LEN(15)
             DCL        VAR(&EMAC) TYPE(*CHAR) LEN(50)
             DCL        VAR(&USF1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USF2) TYPE(*CHAR) LEN(25)
             DCL        VAR(&USF3) TYPE(*DEC)  LEN(15 0)

 /* ---------------------------------------------------------------- */
 /* Declare the parameters for OMQRTVCO                              */
 /* ---------------------------------------------------------------- */
             DCL        VAR(&C@CUSC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C@CCTC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C@TITL) TYPE(*CHAR) LEN(30)
             DCL        VAR(&C@NAMD) TYPE(*CHAR) LEN(50)
             DCL        VAR(&C@TEL#) TYPE(*CHAR) LEN(15)
             DCL        VAR(&C@EMAC) TYPE(*CHAR) LEN(50)
             DCL        VAR(&C@USF1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C@USF2) TYPE(*CHAR) LEN(25)
             DCL        VAR(&C@USF3) TYPE(*DEC)  LEN(15 0)
             DCL        VAR(&C@ERR)  TYPE(*CHAR) LEN(136)

/* ----------------------------------------------------------------- */
/* Program initialisation                                            */
/* ----------------------------------------------------------------- */

          /* ------------------------------------------------------- */
          /* Install the error monitor                               */
          /* ------------------------------------------------------- */
             MONMSG     MSGID(CPF0000 MCH0000) EXEC(GOTO CMDLBL(ERROR))

 /* ---------------------------------------------------------------- */
 /* Call the retrieve API                                            */
 /* ---------------------------------------------------------------- */
             CALL       PGM(OMQRTVCO) PARM(&CUSC &CCTC &C@TITL +
                          &C@NAMD &C@TEL# &C@EMAC &C@USF1 &C@USF2 +
                          &C@USF3 C@ERR '1000000000000000')

/* ----------------------------------------------------------------- */
/* Fill the output parameters if no error.                           */
/* ----------------------------------------------------------------- */
             IF         COND(%SST(&C@ERR 8 1) *NE 'E') THEN(DO)

             CHGVAR     VAR(&CUSC) VALUE(&C@CUSC)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&CCTC) VALUE(&C@CCTC)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&TITL) VALUE(&C@TITL)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&NAMD) VALUE(&C@NAMD)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&TEL#) VALUE(&C@TEL#)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&EMAC) VALUE(&C@EMAC)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&USF1) VALUE(&C@USF1)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&USF2) VALUE(&C@USF2)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&USF3) VALUE(&C@USF3)
             MONMSG     MSGID(MCH3601)

             ENDDO

/* ----------------------------------------------------------------- */
/* Handle return messages                                            */
/* ----------------------------------------------------------------- */

          /* ------------------------------------------------------- */
          /* Clear this programs message queue                       */
          /* ------------------------------------------------------- */
             RMVMSG     CLEAR(*ALL)

          /* ------------------------------------------------------- */
          /* Error                                                   */
          /* ------------------------------------------------------- */
             IF         COND(%SST(&C@ERR 8 1) *EQ 'E') THEN(DO)
             SNDPGMMSG  MSGID(%SST(&C@ERR 1 7)) MSGF(OMSAPI) +
                          MSGDTA(%SST(&C@ERR 9 128)) MSGTYPE(*ESCAPE)
             ENDDO

          /* ------------------------------------------------------- */
          /* Warning                                                 */
          /* ------------------------------------------------------- */
             IF         COND(%SST(&C@ERR 8 1) *EQ 'W') THEN(DO)
             SNDPGMMSG  MSGID(%SST(&C@ERR 1 7)) MSGF(OMSAPI) +
                          MSGDTA(%SST(&C@ERR 9 128)) MSGTYPE(*COMP)
             ENDDO

/* ----------------------------------------------------------------- */
/* Exit.                                                             */
/* ----------------------------------------------------------------- */
             GOTO       CMDLBL(ENDPGM)

/* ----------------------------------------------------------------- */
/* An error occured                                                  */
/* ----------------------------------------------------------------- */
 ERROR:
             SNDPGMMSG  MSGID(OMQ5012) MSGF(OMSAPI) +
                          MSGDTA('OMQRTVCO') MSGTYPE(*ESCAPE)

/* ----------------------------------------------------------------- */
/* End of Program                                                    */
/* ----------------------------------------------------------------- */
 ENDPGM:
             ENDPGM
