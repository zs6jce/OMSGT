 /* ---------------------------------------------------------------- */
 /*                                                                  */
 /*  Program ....: OMQRTVCCCP                                        */
 /*  Description : CPP for Retrieve Customer Call through API        */
 /*  Author .....: Remain Software                                    */
 /*  Module .....: Kernel                                            */
 /*  Date .......: 05 02 1997                                        */
 /*                                                                  */
 /*  Input ......: &HEDC       - Application code                    */
 /*                &CTN#       - Call track number                   */
 /*                                                                  */
 /*  Output .....: &CUSC       - Customer                            */
 /*                &CCTC       - Contact                             */
 /*                &INDT       - Income date                         */
 /*                &INTM       - Income time                         */
 /*                &RCTM       - Completion time                     */
 /*                &USID       - User id                             */
 /*                &CSRC       - Call source                         */
 /*                &USF1       - User field 1                        */
 /*                &USF2       - User field 2                        */
 /*                &USF3       - User field 3                        */
 /*                                                                  */
 /*  Description : This program is the command processor for         */
 /*  program OMQRTVCC. This command allows the user to retrieve      */
 /*  CAL attributes from a CAL in the CAL file.                      */
 /*                                                                  */
 /* ---------------------------------------------------------------- */
             PGM        PARM(&HEDC &CTN# &CUSC &CCTC &INDT &INTM +
                          &RCTM &USID &CSRC &USF1 &USF2 &USF3)

 /* ---------------------------------------------------------------- */
 /* Declare the varaibles                                            */
 /* ---------------------------------------------------------------- */
             DCL        VAR(&HEDC) TYPE(*CHAR) LEN(5)
             DCL        VAR(&CTN#) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CUSC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CCTC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INDT) TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&INTM) TYPE(*DEC)  LEN(6 0)
             DCL        VAR(&RCTM) TYPE(*DEC)  LEN(6 0)
             DCL        VAR(&USID) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSRC) TYPE(*CHAR) LEN(1)
             DCL        VAR(&USF1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USF2) TYPE(*CHAR) LEN(25)
             DCL        VAR(&USF3) TYPE(*DEC)  LEN(15 0)


 /* ---------------------------------------------------------------- */
 /* Declare the parameters for OMQRTVCC                              */
 /* ---------------------------------------------------------------- */
             DCL        VAR(&C@CUSC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C@CCTC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C@INDT) TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&C@INTM) TYPE(*DEC)  LEN(6 0)
             DCL        VAR(&C@RCTM) TYPE(*DEC)  LEN(6 0)
             DCL        VAR(&C@USID) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C@CSRC) TYPE(*CHAR) LEN(1)
             DCL        VAR(&C@USF1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C@USF2) TYPE(*CHAR) LEN(25)
             DCL        VAR(&C@USF3) TYPE(*DEC)  LEN(15 0)
             DCL        VAR(&C@ERR)  TYPE(*CHAR) LEN(136)

/* ----------------------------------------------------------------- */
/* Program initialisation                                            */
/* ----------------------------------------------------------------- */

          /* ------------------------------------------------------- */
          /* Install the error monitor                               */
          /* ------------------------------------------------------- */
             MONMSG     MSGID(CPF0000 MCH0000) EXEC(GOTO CMDLBL(ERROR))

 /* ---------------------------------------------------------------- */
 /* Call the retrieve API                                            */
 /* ---------------------------------------------------------------- */
             CALL       PGM(OMQRTVCC) PARM(&HEDC &CTN# &C@CUSC +
                          &C@CCTC &C@INDT &C@INTM &C@RCTM &C@USID +
                          &C@CSRC &C@USF1 &C@USF2 &C@USF3 C@ERR +
                          '1000000000000000')

/* ----------------------------------------------------------------- */
/* Fill the output parameters if no error.                           */
/* ----------------------------------------------------------------- */
             IF         COND(%SST(&C@ERR 8 1) *NE 'E') THEN(DO)

             CHGVAR     VAR(&CUSC) VALUE(&C@CUSC)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&CCTC) VALUE(&C@CCTC)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&INDT) VALUE(&C@INDT)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&INTM) VALUE(&C@INTM)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&RCTM) VALUE(&C@RCTM)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&USID) VALUE(&C@USID)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&CSRC) VALUE(&C@CSRC)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&USF1) VALUE(&C@USF1)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&USF2) VALUE(&C@USF2)
             MONMSG     MSGID(MCH3601)

             CHGVAR     VAR(&USF3) VALUE(&C@USF3)
             MONMSG     MSGID(MCH3601)

             ENDDO

/* ----------------------------------------------------------------- */
/* Handle return messages                                            */
/* ----------------------------------------------------------------- */

          /* ------------------------------------------------------- */
          /* Clear this programs message queue                       */
          /* ------------------------------------------------------- */
             RMVMSG     CLEAR(*ALL)

          /* ------------------------------------------------------- */
          /* Error                                                   */
          /* ------------------------------------------------------- */
             IF         COND(%SST(&C@ERR 8 1) *EQ 'E') THEN(DO)
             SNDPGMMSG  MSGID(%SST(&C@ERR 1 7)) MSGF(OMSAPI) +
                          MSGDTA(%SST(&C@ERR 9 128)) MSGTYPE(*ESCAPE)
             ENDDO

          /* ------------------------------------------------------- */
          /* Warning                                                 */
          /* ------------------------------------------------------- */
             IF         COND(%SST(&C@ERR 8 1) *EQ 'W') THEN(DO)
             SNDPGMMSG  MSGID(%SST(&C@ERR 1 7)) MSGF(OMSAPI) +
                          MSGDTA(%SST(&C@ERR 9 128)) MSGTYPE(*COMP)
             ENDDO

/* ----------------------------------------------------------------- */
/* Exit.                                                             */
/* ----------------------------------------------------------------- */
             GOTO       CMDLBL(ENDPGM)

/* ----------------------------------------------------------------- */
/* An error occured                                                  */
/* ----------------------------------------------------------------- */
 ERROR:
             SNDPGMMSG  MSGID(OMQ5012) MSGF(OMSAPI) +
                          MSGDTA('OMQRTVCC') MSGTYPE(*ESCAPE)

 /* ---------------------------------------------------------------- */
 /* End of program                                                   */
 /* ---------------------------------------------------------------- */
 ENDPGM:
             RETURN
             ENDPGM
