      *$$\CRTOM CRTDSPF
      *$$\ENDOM
     A*%%TS  SD  19990831  112516  JAN         REL-V3R7M0  5716-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/OMSREFHD)
     A                                      CHGINPDFT
     A                                      HLPTITLE('Jan de Zeeuw')
     A                                      HLPSCHIDX(*LIBL/REMAINHD)
     A                                      HELP
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      ALTHELP(CA01)
     A          R OMH04801                  SFL
     A*%%TS  SD  19990812  160521  JAN         REL-V3R7M0  5716-PW1
     A                                      TEXT('Work with texts (subfile)')
     A  93                                  SFLNXTCHG
     A            FMOPTI         2A  B 12  2TEXT('Option field')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A            FMTXID    R        O 12  7REFFLD(MSID)
     A            FMINCD    R  -20   O 12 17REFFLD(INCD)
     A          R OMH04802                  SFLCTL(OMH04801)
     A*%%TS  SD  19961220  144755  EWOUT       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      TEXT('Work with Texts')
     A                                      CA03
     A                                      CF05
     A                                      CF06
     A                                      CA12
     A  92                                  ROLLUP
     A                                      KEEP
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      OVERLAY
     A                                      ERASE(OMH04801)
     A  90                                  SFLDSP
     A                                      SFLDSPCTL
     A  91                                  SFLCLR
     A N92                                  SFLEND(*MORE)
     A                                      HLPCLR
     A          H                           HLPARA(01 002 01 002)
     A                                      HLPPNLGRP('OMH04802' OMH048)
     A          H                           HLPARA(05 028 05 032)
     A                                      HLPPNLGRP('OMH04802/FMHEDC' OMH048)
     A          H                           HLPARA(07 028 07 037)
     A                                      HLPPNLGRP('OMH04802/FMPOS' OMH048)
     A          H                           HLPARA(12 002 20 003)
     A                                      HLPPNLGRP('OMH04801/FMOPTI' OMH048)
     A          H                           HLPARA(23 001 23 080)
     A                                      HLPPNLGRP('OMH04802/KEYS' OMH048)
     A            FMFLDC        10A  H
     A            FMFMTC        10A  H
     A            CRRN01         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'OMH048  '
     A            MH04800       50A  O  1 16TEXT('Text id maintenance')
     A                                      MSGID(MH0 4800 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH04801'
     A            MH04801       50A  O  2 16TEXT('Work with texts')
     A                                      MSGID(MH0 4801 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            HED0001       25A  O  5  2TEXT('HelpDesk code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        O  5 28REFFLD(HEDC)
     A                                      TEXT('HelpDesk code')
     A                                      DSPATR(HI)
     A            FMHEDD    R   40   O  5 35REFFLD(HEDD)
     A                                      TEXT('HelpDesk Description')
     A                                      DSPATR(HI)
     A            POS0001       25A  O  7  2TEXT('Position at')
     A                                      MSGID(POS 0001 OMHDSP)
     A            FMPOS     R        B  7 28REFFLD(MSID)
     A                                      DSPATR(UL)
     A            MH04804       70A  O  9  2TEXT('Options')
     A                                      MSGID(MH0 4804 OMHDSP)
     A                                      COLOR(BLU)
     A            MH04802       68A  O 11  2TEXT('Subfile header')
     A                                      MSGID(MH0 4802 OMHDSP)
     A                                      DSPATR(HI)
     A          R OMH04803
     A*%%TS  SD  19961010  113532  WIM         REL-V3R1M0  5763-PW1
     A                                      TEXT('Work with Texts (footer)')
     A                                      CA03
     A                                      CF05
     A                                      CF06
     A                                      CA12
     A                                      ROLLUP(92)
     A                                      OVERLAY
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            KEY0064       78A  O 23  2TEXT('F3/F5/F6/F12 F21=Print list')
     A                                      MSGID(KEY 0064 OMHDSP)
     A                                      COLOR(BLU)
     A          R OMH04804
     A*%%TS  SD  19990824  094354  JAN         REL-V3R7M0  5716-PW1
     A                                      TEXT('Maintain Text')
     A                                      CA03
     A                                      CA12
     A                                      CHANGE(40)
     A N80
     AON81                                  CF04
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      INVITE
     A                                      OVERLAY
     A                                      HLPCLR
     A          H                           HLPPNLGRP(OMH04804 OMH048)
     A                                      HLPARA(01 002 01 002)
     A          H                           HLPPNLGRP('OMH04804/FMSTRC' OMH048)
     A                                      HLPARA(07 028 07 037)
     A          H                           HLPPNLGRP('OMH04804/FMSTRD' OMH048)
     A                                      HLPARA(08 028 08 077)
     A          H                           HLPPNLGRP('OMH04804/FMSTRI' OMH048)
     A                                      HLPARA(10 028 10 028)
     A          H                           HLPPNLGRP('OMH04804/KEYS' OMH048)
     A                                      HLPARA(23 001 23 080)
     A            FMFMTC        10A  H
     A            FMFLDC        10A  H
     A                                  1  2'OMH048  '
     A            MH04800       50A  O  1 16TEXT('Text Maintenance')
     A                                      MSGID(MH0 4800 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH04802'
     A            MH04803       50A  O  2 16TEXT('Maintain Text')
     A                                      MSGID(MH0 4803 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A N80N81     ACT0001       10A  O  4  2MSGID(ACT 0001 OMHDSP)
     A                                      DSPATR(RI)
     A  80N81     ACT0002       10A  O  4  2MSGID(ACT 0002 OMHDSP)
     A                                      DSPATR(RI)
     A  80 81     ACT0003       10A  O  4  2MSGID(ACT 0003 OMHDSP)
     A                                      DSPATR(RI)
     A            APP0001       25A  O  5  2TEXT('HelpDesk Code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        O  5 28REFFLD(HEDC)
     A                                      TEXT('Application code')
     A                                      DSPATR(HI)
     A            FMHEDD    R   40   O  5 35REFFLD(HEDD)
     A                                      DSPATR(HI)
     A  80
     AON80        ROU0002       25A  O  7  2MSGID(TXT 0006 OMHDSP)
     A            FMTXID    R        B  7 28REFFLD(MSID)
     A  80                                  DSPATR(HI)
     A  70                                  DSPATR(RI)
     A N80                                  DSPATR(UL)
     A  70                                  DSPATR(PC)
     A  80                                  DSPATR(PR)
     A                                      TEXT('Text id')
     A            DSC0004       25A  O  8  2TEXT('Description')
     A                                      MSGID(DSC 0004 OMHDSP)
     A            FMINCD    R  -20   B  8 28REFFLD(INCD)
     A N81                                  DSPATR(UL)
     A  81                                  DSPATR(HI)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A  81                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      TEXT('Description')
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            KEY0003       78A  O 23  2TEXT('F3=Exit  F12=Cancel')
     A                                      MSGID(KEY 0003 OMHDSP)
     A                                      COLOR(BLU)
     A            TXT007        25   O  9  2TEXT('Kind of text')
     A                                      MSGID(TXT 0007 OMHDSP)
     A            SUP002        25   O 10  2TEXT('Support level')
     A                                      MSGID(SUP 0002 OMHDSP)
     A            FMITXT    R        B  9 28REFFLD(ITXT)
     A N81                                  DSPATR(UL)
     A  81                                  DSPATR(HI)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A  81                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      TEXT('Kind of text')
     A            FMSUL#    R        B 10 28REFFLD(ITXT)
     A N81                                  DSPATR(UL)
     A  81                                  DSPATR(HI)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A  81                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      TEXT('Support level')
     A          R MSGSFL
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A          R MSGCTL
     A                                      SFLCTL(MSGSFL)
     A                                      SFLINZ
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N91                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
     A          R OMH04805                  SFL
     A*%%TS  SD  19990825  114649  JAN         REL-V3R7M0  5716-PW1
     A                                      TEXT('Work with texts (subfile)')
     A  93                                  SFLNXTCHG
     A            FMOPTI         2A  B 12  2TEXT('Option field')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A            FMINCD    R  -20   O 12 11REFFLD(INCD)
     A            FMITXT    R        O 12  7REFFLD(ITXT)
     A                                      TEXT('Text type')
     A            FMHDR#    R        O 12 63REFFLD(SEQ#)
     A                                      DSPATR(ND)
     A            FMSUL#    R        O 12 72REFFLD(SUL#)
     A                                      DSPATR(ND)
     A          R OMH04806                  SFLCTL(OMH04805)
     A*%%TS  SD  19990825  114649  JAN         REL-V3R7M0  5716-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      TEXT('Work with Texts')
     A                                      CA03
     A                                      CF05
     A                                      CF06
     A                                      CA12
     A  92                                  ROLLUP
     A                                      KEEP
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      OVERLAY
     A                                      ERASE(OMH04801)
     A  90                                  SFLDSP
     A                                      SFLDSPCTL
     A  91                                  SFLCLR
     A N92                                  SFLEND(*MORE)
     A                                      HLPCLR
     A          H                           HLPPNLGRP(OMH04802 OMH048)
     A                                      HLPARA(01 002 01 002)
     A          H                           HLPPNLGRP('OMH04802/FMHEDC' OMH048)
     A                                      HLPARA(05 028 05 032)
     A          H                           HLPPNLGRP('OMH04802/FMPOS' OMH048)
     A                                      HLPARA(07 028 07 037)
     A          H                           HLPPNLGRP('OMH04801/FMOPTI' OMH048)
     A                                      HLPARA(12 002 20 003)
     A          H                           HLPPNLGRP('OMH04802/KEYS' OMH048)
     A                                      HLPARA(23 001 23 080)
     A            FMFLDC        10A  H
     A            FMFMTC        10A  H
     A            CRRN04         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'OMH048  '
     A            MH04800       50A  O  1 16TEXT('Text id maintenance')
     A                                      MSGID(MH0 4800 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH04805'
     A            MH04801       50A  O  2 16TEXT('Work with texts')
     A                                      MSGID(MH0 4805 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            HED0001       25A  O  5  2TEXT('HelpDesk code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        O  5 28REFFLD(HEDC)
     A                                      TEXT('HelpDesk code')
     A                                      DSPATR(HI)
     A            FMHEDD    R   40   O  5 37REFFLD(HEDD)
     A                                      TEXT('HelpDesk Description')
     A                                      DSPATR(HI)
     A            MH04804       70A  O  9  2TEXT('Options')
     A                                      MSGID(MH0 4804 OMHDSP)
     A                                      COLOR(BLU)
     A            MH04806       68A  O 11  2TEXT('Subfile header')
     A                                      MSGID(MH0 4806 OMHDSP)
     A                                      DSPATR(HI)
     A            FLD001        25A  O  7  2TEXT('HelpDesk code')
     A                                      MSGID(TXT 0006 OMHDSP)
     A            FMTXI4    R        O  7 28REFFLD(MSID)
     A                                      TEXT('Text id')
     A                                      DSPATR(HI)
     A            FMINC4    R  -20   O  7 37REFFLD(INCD)
     A                                      TEXT('Text Description')
     A                                      DSPATR(HI)
     A          R OMH04807
     A*%%TS  SD  19990831  112516  JAN         REL-V3R7M0  5716-PW1
     A                                      TEXT('Maintain Text')
     A                                      CA03
     A                                      CA12
     A                                      CHANGE(40)
     A N80
     AON81                                  CF04
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      INVITE
     A                                      OVERLAY
     A                                      HLPCLR
     A          H                           HLPPNLGRP(OMH04804 OMH048)
     A                                      HLPARA(01 002 01 002)
     A          H                           HLPPNLGRP('OMH04804/FMSTRC' OMH048)
     A                                      HLPARA(07 028 07 037)
     A          H                           HLPPNLGRP('OMH04804/FMSTRD' OMH048)
     A                                      HLPARA(08 028 08 077)
     A          H                           HLPPNLGRP('OMH04804/FMSTRI' OMH048)
     A                                      HLPARA(10 028 10 028)
     A          H                           HLPPNLGRP('OMH04804/KEYS' OMH048)
     A                                      HLPARA(23 001 23 080)
     A            FMFMTC        10A  H
     A            FMFLDC        10A  H
     A                                  1  2'OMH048  '
     A            MH04800       50A  O  1 16TEXT('Text Maintenance')
     A                                      MSGID(MH0 4800 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH04802'
     A            MH04803       50A  O  2 16TEXT('Maintain Text')
     A                                      MSGID(MH0 4803 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A N80N81     ACT0001       10A  O  4  2MSGID(ACT 0001 OMHDSP)
     A                                      DSPATR(RI)
     A  80N81     ACT0002       10A  O  4  2MSGID(ACT 0002 OMHDSP)
     A                                      DSPATR(RI)
     A  80 81     ACT0003       10A  O  4  2MSGID(ACT 0003 OMHDSP)
     A                                      DSPATR(RI)
     A            APP0001       25A  O  5  2TEXT('HelpDesk Code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        O  5 28REFFLD(HEDC)
     A                                      TEXT('Application code')
     A                                      DSPATR(HI)
     A            FMHEDD    R   40   O  5 37REFFLD(HEDD)
     A                                      DSPATR(HI)
     A  80
     AON80        ROU0002       25A  O  6  2MSGID(TXT 0006 OMHDSP)
     A            FMTXID    R        B  6 28REFFLD(MSID)
     A  80                                  DSPATR(HI)
     A  70                                  DSPATR(RI)
     A N80                                  DSPATR(UL)
     A  70                                  DSPATR(PC)
     A  80                                  DSPATR(PR)
     A                                      TEXT('Text id')
     A            DSC0006       25A  O  8  2TEXT('Description bloc')
     A                                      MSGID(DSC 0006 OMHDSP)
     A            FMINCD    R  -20   B  8 28REFFLD(INCD)
     A N81                                  DSPATR(UL)
     A  81                                  DSPATR(HI)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A  81                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      TEXT('Description')
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            KEY0003       78A  O 23  2TEXT('F3=Exit  F12=Cancel')
     A                                      MSGID(KEY 0003 OMHDSP)
     A                                      COLOR(BLU)
     A            TXT007        25A  O  9  2TEXT('Kind of text')
     A                                      MSGID(TXT 0007 OMHDSP)
     A            SUP002        25A  O 10  2TEXT('Support level')
     A                                      MSGID(SUP 0002 OMHDSP)
     A            FMITXT    R        B  9 28REFFLD(ITXT)
     A N81                                  DSPATR(UL)
     A  81                                  DSPATR(HI)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A  81                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      TEXT('Kind of text')
     A            FMSUL#    R        B 10 28REFFLD(ITXT)
     A N81                                  DSPATR(UL)
     A  81                                  DSPATR(HI)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A  81                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      TEXT('Support level')
     A            FMINC4    R  -20   O  6 37REFFLD(INCD)
     A                                      DSPATR(HI)
