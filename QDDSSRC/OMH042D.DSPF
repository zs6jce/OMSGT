      *$$\CRTOM CRTDSPF
      *$$\ENDOM
     A*%%TS  SD  20000712  131739  WIM         REL-V3R7M0  5716-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/OMSREFHD)
     A                                      CHGINPDFT
     A                                      HLPTITLE('Jan de Zeeuw')
     A                                      HLPSCHIDX(*LIBL/REMAINHD)
     A                                      HELP
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      ALTHELP(CA01)
     A          R OMH04201                  SFL
     A*%%TS  SD  20000711  154940  WIM         REL-V3R7M0  5716-PW1
     A                                      TEXT('Work with customers')
     A  93                                  SFLNXTCHG
     A            FMACPV        11A  O 10  2
     A            FMMATM    R     A  B 10 15REFFLD(MATM)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A N82N81                               DSPATR(UL)
     A  81
     AO 82                                  DSPATR(PR)
     A            FMDFTI         1A  H
     A            FMOVRD        10   O 10 27TEXT('Override description')
     A            FMDESC    R   40   O 10 39REFFLD(DESC)
     A                                      TEXT('Description')
     A          R OMH04202                  SFLCTL(OMH04201)
     A*%%TS  SD  20000712  131739  WIM         REL-V3R7M0  5716-PW1
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      TEXT('Work with contacts')
     A                                      CA03
     A                                      CF06
     A                                      CA12
     A                                      CA05
     A  92                                  ROLLUP(22)
     A                                      ROLLDOWN(26)
     A                                      CF08
     A                                      KEEP
     A                                      OVERLAY
     A                                      HLPCLR
     A                                      SFLCSRRRN(&FMSCRR)
     A  90                                  SFLDSP
     A                                      SFLDSPCTL
     A  91                                  SFLCLR
     A N92                                  SFLEND(*MORE)
     A          H                           HLPPNLGRP(OMH04201 OMH042)
     A                                      HLPARA(01 002 01 002)
     A          H                           HLPPNLGRP('OMH04201/FMACPV' OMH042)
     A                                      HLPARA(07 028 07 037)
     A          H                           HLPPNLGRP('OMH04201/FMMATM' OMH042)
     A                                      HLPARA(12 002 20 003)
     A          H                           HLPPNLGRP('OMH04201/FMDFTI' OMH042)
     A                                      HLPARA(21 002 21 003)
     A          H                           HLPPNLGRP('OMH04201/KEYS' OMH042)
     A                                      HLPARA(23 001 23 080)
     A            FMSCRR         5S 0H
     A            CRRN01         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'OMH042  '
     A  50        MH01201       50A  O  1 15TEXT('Maintain Contact')
     A                                      MSGID(MH0 1201 OMHDSP)
     A                                      DSPATR(HI)
     A  51        MH04203       50A  O  1 15TEXT('Maintain Customer')
     A                                      MSGID(MH0 4203 OMHDSP)
     A                                      DSPATR(HI)
     A  52        MH04204       50A  O  1 15TEXT('Maintain HelpDesk')
     A                                      MSGID(MH0 4204 OMHDSP)
     A                                      DSPATR(HI)
     A  53        MH04400       50A  O  1 15TEXT('Maintain System')
     A                                      MSGID(MH0 4400 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH04202'
     A            MH04201       50A  O  2 15TEXT('Maintain Template')
     A                                      MSGID(MH0 4201 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A N80N81     ACT0001       10A  O  4  2MSGID(ACT 0001 OMHDSP)
     A                                      DSPATR(RI)
     A  80N81     ACT0002       10A  O  4  2MSGID(ACT 0002 OMHDSP)
     A                                      DSPATR(RI)
     A  80 81N82  ACT0003       10A  O  4  2MSGID(ACT 0003 OMHDSP)
     A                                      DSPATR(RI)
     A  80 81 82  ACT0004       10A  O  4  2MSGID(ACT 0004 OMHDSP)
     A                                      DSPATR(RI)
     A                                      DSPATR(UL)
     A  50
     AO 51        CUS0003       25A  O  6  2MSGID(CUS 0003 OMHDSP)
     A  52        HED0001       25A  O  6  2MSGID(HED 0001 OMHDSP)
     A  53        SYS0004       25A  O  6  2TEXT('Customer contact code')
     A                                      MSGID(SYS 0003 OMHDSP)
     A  50
     AO 51        FMCUSC    R        O  6 28REFFLD(CUSC)
     A                                      DSPATR(HI)
     A  52        FMHEDC    R        O  6 28REFFLD(HEDC)
     A                                      DSPATR(HI)
     A  50        CUS0004       25A  O  7  2TEXT('Customer contact code')
     A                                      MSGID(CUS 0004 OMHDSP)
     A  50        FMCCTC    R        O  7 28REFFLD(CCTC)
     A                                      DSPATR(HI)
     A            MH04206       79   O  9  2TEXT('Subfile header')
     A                                      MSGID(MH0 4206 OMHDSP)
     A                                      DSPATR(HI)
     A          R OMH04203
     A*%%TS  SD  20000712  131739  WIM         REL-V3R7M0  5716-PW1
     A                                      TEXT('Work with Application Interfa-
     A                                      ces')
     A                                      OVERLAY
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            MH04207       78   O 23  2MSGID(MH0 4207 OMHDSP)
     A                                      COLOR(BLU)
     A          R OMH04204
     A                                      ROLLDOWN(26)
     A                                      CA03
     A                                      CA12
     A                                      KEEP
     A                                      OVERLAY
     A                                      HLPCLR
     A          H                           HLPPNLGRP(OMH04201 OMH042)
     A                                      HLPARA(01 002 01 002)
     A          H                           HLPPNLGRP('OMH04201/FMACPV' OMH042)
     A                                      HLPARA(07 028 07 037)
     A          H                           HLPPNLGRP('OMH04201/FMMATM' OMH042)
     A                                      HLPARA(12 002 20 003)
     A          H                           HLPPNLGRP('OMH04201/FMDFTI' OMH042)
     A                                      HLPARA(21 002 21 003)
     A          H                           HLPPNLGRP('OMH04201/KEYS' OMH042)
     A                                      HLPARA(23 001 23 080)
     A                                  1  2'OMH042  '
     A  50        MH01201       50A  O  1 15TEXT('Maintain Contact')
     A                                      MSGID(MH0 1201 OMHDSP)
     A                                      DSPATR(HI)
     A  51        MH04203       50A  O  1 15TEXT('Maintain Contact')
     A                                      MSGID(MH0 4203 OMHDSP)
     A                                      DSPATR(HI)
     A  52        MH04204       50A  O  1 15TEXT('Maintain Contact')
     A                                      MSGID(MH0 4204 OMHDSP)
     A                                      DSPATR(HI)
     A  53        MH04400       50A  O  1 15TEXT('Maintain Contact')
     A                                      MSGID(MH0 4400 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH04202'
     A            MH04201       50A  O  2 15TEXT('Maintain Contact')
     A                                      MSGID(MH0 4201 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A N80N81     ACT0001       10A  O  4  2MSGID(ACT 0001 OMHDSP)
     A                                      DSPATR(RI)
     A  80N81     ACT0002       10A  O  4  2MSGID(ACT 0002 OMHDSP)
     A                                      DSPATR(RI)
     A  80 81N82  ACT0003       10A  O  4  2MSGID(ACT 0003 OMHDSP)
     A                                      DSPATR(RI)
     A  80 81 82  ACT0004       10A  O  4  2MSGID(ACT 0004 OMHDSP)
     A                                      DSPATR(RI)
     A                                      DSPATR(UL)
     A  53
     AO 52        MH04205       79A  O  9  2TEXT('Subfile header')
     A                                      MSGID(MH0 4205 OMHDSP)
     A                                      DSPATR(HI)
     A  50        MH04206       79A  O  9  2TEXT('Subfile header')
     A                                      MSGID(MH0 4206 OMHDSP)
     A                                      DSPATR(HI)
     A  51        MH04207       79A  O  9  2TEXT('Subfile header')
     A                                      MSGID(MH0 4207 OMHDSP)
     A                                      DSPATR(HI)
     A  50
     AO 51        CUS0003       25A  O  6  2MSGID(CUS 0003 OMHDSP)
     A  50
     AO 51        FMCUSC    R        O  6 28REFFLD(CUSC)
     A                                      DSPATR(HI)
     A  52        HED0001       25A  O  6  2MSGID(HED 0001 OMHDSP)
     A  52        FMHEDC    R        O  6 28REFFLD(HEDC)
     A                                      DSPATR(HI)
     A  53        SYS0004       25A  O  6  2TEXT('Customer contact code')
     A                                      MSGID(SYS 0003 OMHDSP)
     A  50        CUS0004       25A  O  7  2TEXT('Customer contact code')
     A                                      MSGID(CUS 0004 OMHDSP)
     A  50        FMCCTC    R        O  7 28REFFLD(CCTC)
     A                                      DSPATR(HI)
     A            MH04202       25A  O 11  4TEXT('No customers')
     A                                      MSGID(MH0 4202 OMHDSP)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            KEY0066       78   O 23  2TEXT('F3=Exit  F5=Refresh  F6=Add  -
     A                                      F12=Cancel')
     A                                      MSGID(KEY 0066 OMHDSP)
     A                                      COLOR(BLU)
     A          R MSGSFL                    SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      SFLINZ
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N91                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
     A
