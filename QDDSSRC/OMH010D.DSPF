      *$$\CRTOM CRTDSPF
      *$$\ENDOM
     A                                      HLPTITLE('Remain Software')
     A                                      HLPSCHIDX(REMAINHD)
     A                                      HELP
     A*%%TS  SD  19970304  131832  EWOUT       REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/OMSREFHD)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      ALTHELP(CA01)
     A          R OMH01001                  SFL
     A*%%TS  SD  19961120  140550  EWOUT       REL-V3R1M0  5763-PW1
     A                                      TEXT('Work with Status Actions (sub-
     A                                      file)')
     A  93                                  SFLNXTCHG
     A            FMEOEI         1A  H      TEXT('Entry or exit indicator')
     A            FMOPTI         1A  B 14  3TEXT('Option field')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A            FMEOED         6A  O 14  7TEXT('Entry or exit description')
     A            FMASQ#    R        O 14 20REFFLD(ASQ#)
     A                                      TEXT('Status action sequence')
     A            FMSEPG    R        O 14 26REFFLD(SEPG)
     A                                      TEXT('Exit program name')
     A            FMSEPL    R        O 14 38REFFLD(SEPL)
     A                                      TEXT('Exit program library')
     A            FMINTL    R        O 14 50REFFLD(INTL)
     A                                      TEXT('Interface level')
     A          R OMH01002                  SFLCTL(OMH01001)
     A*%%TS  SD  19961120  140550  EWOUT       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      TEXT('Work with Status Actions')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A  92                                  ROLLUP
     A                                      KEEP
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      OVERLAY
     A                                      ERASE(OMH01001)
     A  90                                  SFLDSP
     A                                      SFLDSPCTL
     A  91                                  SFLCLR
     A N92                                  SFLEND(*MORE)
     A                                      HLPCLR
     A          H                           HLPARA(01 002 01 002)
     A                                      HLPPNLGRP('OMH01002' OMH010)
     A          H                           HLPARA(08 028 08 033)
     A                                      HLPPNLGRP('OMH01002/FMSEL' OMH010)
     A          H                           HLPARA(09 028 09 030)
     A                                      HLPPNLGRP('OMH01002/FMPOS' OMH010)
     A          H                           HLPARA(14 003 20 003)
     A                                      HLPPNLGRP('OMH01001/FMOPTI' OMH010)
     A          H                           HLPARA(23 001 23 080)
     A                                      HLPPNLGRP('OMH01002/KEYS' OMH010)
     A            FMFLDC        10A  H
     A            FMFMTC        10A  H
     A            CRRN01         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'OMH010  '
     A            MH01000       50A  O  1 16TEXT('Status Action Maintenance')
     A                                      MSGID(MH0 1000 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH01001'
     A            MH01001       50A  O  2 16TEXT('Work with Status Actions')
     A                                      MSGID(MH0 1001 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            HED0001       25A  O  5  2TEXT('HelpDesk code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        O  5 28REFFLD(HEDC)
     A                                      TEXT('HelpDesk code')
     A                                      DSPATR(HI)
     A            FMHEDD    R   40   O  5 35REFFLD(HEDD)
     A                                      TEXT('HelpDesk Description')
     A                                      DSPATR(HI)
     A            STA0001       25A  O  6  2TEXT('Status')
     A                                      MSGID(STA 0001 OMHDSP)
     A            FMSTAT    R        O  6 28REFFLD(STAT)
     A                                      TEXT('Status')
     A                                      DSPATR(HI)
     A            FMSTAD    R   40   O  6 35REFFLD(STAD)
     A                                      TEXT('Status description')
     A                                      DSPATR(HI)
     A            EOE0001       25A  O  8  2TEXT('Entry or exit')
     A                                      MSGID(EOE 0001 OMHDSP)
     A            FMSEL          6A  B  8 28DSPATR(UL)
     A                                      TEXT('Select type')
     A            POS0001       25A  O  9  2TEXT('Position at')
     A                                      MSGID(POS 0001 OMHDSP)
     A            FMPOS     R     Y  B  9 28REFFLD(ASQ#)
     A                                      TEXT('Position at')
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A            MH01004       70A  O 11  2TEXT('Options')
     A                                      MSGID(MH0 1004 OMHDSP)
     A                                      COLOR(BLU)
     A            MH01002       63A  O 13  2TEXT('Subfile header')
     A                                      MSGID(MH0 1002 OMHDSP)
     A                                      DSPATR(HI)
     A            CHO0058       40A  O  8 35MSGID(CHO 0058 OMHDSP)
     A          R OMH01003
     A*%%TS  SD  19961011  152107  EWOUT       REL-V3R1M0  5763-PW1
     A                                      TEXT('Work with Status Actions (foo-
     A                                      ter)')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A                                      ROLLUP(92)
     A                                      OVERLAY
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            MH01003       78A  O 23  2TEXT('Command keys')
     A                                      MSGID(MH0 1003 OMHDSP)
     A                                      COLOR(BLU)
     A          R OMH01004
     A*%%TS  SD  19970304  131832  EWOUT       REL-V3R1M0  5763-PW1
     A                                      TEXT('Maintain Status Action')
     A                                      CF03
     A                                      CF12
     A                                      CHANGE(40)
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      INVITE
     A                                      OVERLAY
     A                                      HLPCLR
     A          H                           HLPARA(01 002 01 002)
     A                                      HLPPNLGRP('OMH01004' OMH010)
     A          H                           HLPARA(08 028 08 028)
     A                                      HLPPNLGRP('OMH01004/FMEOEI' OMH010)
     A          H                           HLPARA(09 028 09 030)
     A                                      HLPPNLGRP('OMH01004/FMASQY' OMH010)
     A          H                           HLPARA(11 028 11 037)
     A                                      HLPPNLGRP('OMH01004/FMSEPG' OMH010)
     A          H                           HLPARA(12 028 12 037)
     A                                      HLPPNLGRP('OMH01004/FMSEPL' OMH010)
     A          H                           HLPARA(14 028 14 035)
     A                                      HLPPNLGRP('OMH01004/FMINTL' OMH010)
     A          H                           HLPARA(23 001 23 080)
     A                                      HLPPNLGRP('OMH01004/KEYS' OMH010)
     A            FMFMTC        10A  H
     A            FMFLDC        10A  H
     A                                  1  2'OMH010  '
     A            MH01000       50A  O  1 16TEXT('Status Action Maintenance')
     A                                      MSGID(MH0 1000 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH01002'
     A            MH01006       50A  O  2 16TEXT('Maintain Status Action')
     A                                      MSGID(MH0 1006 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A N80N81     ACT0001       10A  O  4  2MSGID(ACT 0001 OMHDSP)
     A                                      DSPATR(RI)
     A  80N81     ACT0002       10A  O  4  2MSGID(ACT 0002 OMHDSP)
     A                                      DSPATR(RI)
     A  80 81     ACT0003       10A  O  4  2MSGID(ACT 0003 OMHDSP)
     A                                      DSPATR(RI)
     A            HED0001       25A  O  5  2TEXT('HelpDesk code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        O  5 28REFFLD(HEDC)
     A                                      TEXT('HelpDesk code')
     A                                      DSPATR(HI)
     A            FMHEDD    R   40   O  5 35REFFLD(HEDD)
     A                                      TEXT('HelpDesk Description')
     A                                      DSPATR(HI)
     A            STA0001       25A  O  6  2TEXT('Status')
     A                                      MSGID(STA 0001 OMHDSP)
     A            FMSTAT    R        O  6 28REFFLD(STAT)
     A                                      TEXT('Status')
     A                                      DSPATR(HI)
     A            FMSTAD    R   40   O  6 35REFFLD(STAD)
     A                                      TEXT('Status description')
     A                                      DSPATR(HI)
     A            EOE0001       25A  O  8  2TEXT('Entry or exit')
     A                                      MSGID(EOE 0001 OMHDSP)
     A            FMEOEI    R        B  8 28REFFLD(EOEI)
     A                                      TEXT('Entry or exit indicator')
     A  80                                  DSPATR(HI)
     A  70                                  DSPATR(RI)
     A N80                                  DSPATR(UL)
     A  70                                  DSPATR(PC)
     A  80                                  DSPATR(PR)
     A            CHO0061       40A  O  8 35TEXT('0=Entry, 1=Exit')
     A                                      MSGID(CHO 0061 OMHDSP)
     A            SEQ0001       25A  O  9  2TEXT('Sequence number')
     A                                      MSGID(SEQ 0001 OMHDSP)
     A            FMASQ#    R     Y  B  9 28REFFLD(ASQ#)
     A                                      TEXT('Status action sequence')
     A  80                                  DSPATR(HI)
     A  71                                  DSPATR(RI)
     A N80                                  DSPATR(UL)
     A  71                                  DSPATR(PC)
     A  80                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A            PGM0002       25A  O 11  2TEXT('Program name')
     A                                      MSGID(PGM 0002 OMHDSP)
     A            FMSEPG    R        B 11 28REFFLD(SEPG)
     A                                      TEXT('Exit program name')
     A  81                                  DSPATR(HI)
     A  72                                  DSPATR(RI)
     A N81                                  DSPATR(UL)
     A  72                                  DSPATR(PC)
     A  81                                  DSPATR(PR)
     A            NAM0001       40A  O 11 40TEXT('Name')
     A                                      MSGID(NAM 0001 OMHDSP)
     A            LIB0002       25A  O 12  2TEXT('Library')
     A                                      MSGID(LIB 0002 OMHDSP)
     A            FMSEPL    R        B 12 28REFFLD(SEPL)
     A                                      TEXT('Exit program library')
     A  81                                  DSPATR(HI)
     A  73                                  DSPATR(RI)
     A N81                                  DSPATR(UL)
     A  73                                  DSPATR(PC)
     A  81                                  DSPATR(PR)
     A            CHO0042       40A  O 12 40TEXT('Name, *LIBL')
     A                                      MSGID(CHO 0042 OMHDSP)
     A            LEV0008       25A  O 14  2TEXT('OMS Interface level')
     A                                      MSGID(LEV 0008 OMHDSP)
     A            FMINTL    R        B 14 28REFFLD(INTL)
     A                                      TEXT('Interface level')
     A  81                                  DSPATR(HI)
     A  74                                  DSPATR(RI)
     A N81                                  DSPATR(UL)
     A  74                                  DSPATR(PC)
     A  81                                  DSPATR(PR)
     A            CHO0037       40   O 14 40TEXT('*V1R1M0')
     A                                      MSGID(VRS 0011 OMHDSP)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            MH01005       78A  O 23  2TEXT('F3=Exit  F12=Cancel')
     A                                      MSGID(MH0 1005 OMHDSP)
     A                                      COLOR(BLU)
     A          R MSGSFL                    SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A
     A          R MSGCTL
     A                                      SFLCTL(MSGSFL)
     A                                      SFLINZ
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N91                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
