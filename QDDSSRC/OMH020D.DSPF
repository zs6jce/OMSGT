      *$$\CRTOM CRTDSPF
      *$$\ENDOM
     A*%%TS  SD  19970728  105909  JAN         REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/OMSREFHD)
     A                                      CHGINPDFT
     A                                      HLPTITLE('Jan de Zeeuw')
     A                                      HLPSCHIDX(*LIBL/REMAINHD)
     A                                      HELP
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      ALTHELP(CA01)
     A          R OMH02001                  SFL
     A*%%TS  SD  19961010  113532  WIM         REL-V3R1M0  5763-PW1
     A                                      TEXT('Work with Routes (subfile)')
     A  93                                  SFLNXTCHG
     A            FMOPTI         2A  B 13  2TEXT('Option field')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A            FMSTRC    R        O 13  7REFFLD(STRC)
     A            FMSTRD    R        O 13 20REFFLD(STRD)
     A          R OMH02002                  SFLCTL(OMH02001)
     A*%%TS  SD  19970728  105909  JAN         REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      TEXT('Work with Routes')
     A                                      CF03
     A                                      CF05
     A N43                                  CF06
     A                                      CF12
     A  92                                  ROLLUP
     A                                      CF21
     A                                      KEEP
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      OVERLAY
     A  90                                  SFLDSP
     A                                      SFLDSPCTL
     A  91                                  SFLCLR
     A N92                                  SFLEND(*MORE)
     A                                      HLPCLR
     A          H                           HLPPNLGRP(OMH02002 OMH020)
     A                                      HLPARA(01 002 01 002)
     A          H                           HLPPNLGRP('OMH02002/FMHEDC' OMH020)
     A                                      HLPARA(05 028 05 032)
     A          H                           HLPPNLGRP('OMH02002/FMPOS' OMH020)
     A                                      HLPARA(07 028 07 037)
     A          H                           HLPPNLGRP('OMH02001/FMOPTI' OMH020)
     A                                      HLPARA(12 002 20 003)
     A          H                           HLPPNLGRP('OMH02002/KEYS' OMH020)
     A                                      HLPARA(23 001 23 080)
     A            FMFLDC        10A  H
     A            FMFMTC        10A  H
     A            CRRN01         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'OMH020  '
     A            MH02000       50A  O  1 16TEXT('Question list maintenance')
     A                                      MSGID(MH0 2000 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH02001'
     A            MH02001       50A  O  2 16TEXT('Work with question lists')
     A                                      MSGID(MH0 2001 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            HED0001       25A  O  5  2TEXT('HelpDesk code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        O  5 28REFFLD(HEDC)
     A                                      TEXT('HelpDesk code')
     A                                      DSPATR(HI)
     A            FMHEDD    R   40   O  5 40REFFLD(HEDD)
     A                                      TEXT('HelpDesk Description')
     A                                      DSPATR(HI)
     A            POS0001       25A  O  8  2TEXT('Position at')
     A                                      MSGID(POS 0001 OMHDSP)
     A            FMPOS     R        B  8 28REFFLD(STRC)
     A                                      DSPATR(UL)
     A            MH02004       70A  O 10  2TEXT('Options')
     A                                      MSGID(MH0 2004 OMHDSP)
     A                                      COLOR(BLU)
     A            MH02002       68A  O 12  2TEXT('Subfile header')
     A                                      MSGID(MH0 2002 OMHDSP)
     A                                      DSPATR(HI)
     A            EMP001        25A  O  6  2TEXT('Employee code')
     A                                      MSGID(EMP 0001 OMHDSP)
     A            FMEMPC    R        O  6 28REFFLD(USID)
     A                                      TEXT('Employee code')
     A                                      DSPATR(HI)
     A            FMUSDC    R   40   O  6 40REFFLD(USDC)
     A                                      TEXT('HelpDesk Description')
     A                                      DSPATR(HI)
     A  43        MH2008        40   O  7 40MSGID(MH0 2008 OMHDSP)
     A                                      DSPATR(HI)
     A          R OMH02003
     A*%%TS  SD  19970708  150016  JAN         REL-V3R1M0  5763-PW1
     A                                      TEXT('Work with Routes (footer)')
     A                                      CF03
     A                                      CF05
     A N43                                  CF06
     A                                      CF12
     A                                      ROLLUP(92)
     A                                      OVERLAY
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A N43        KEY0064       78A  O 23  2TEXT('F3/F5/F6/F12 F21=Print list')
     A                                      MSGID(KEY 0064 OMHDSP)
     A                                      COLOR(BLU)
     A  43        KEY0063       78A  O 23  2TEXT('F3/F5/F6/F12 F21=Print list')
     A                                      MSGID(KEY 0063 OMHDSP)
     A                                      COLOR(BLU)
     A          R OMH02004
     A*%%TS  SD  19970715  153510  JAN         REL-V3R1M0  5763-PW1
     A                                      TEXT('Maintain Status Route')
     A                                      CF03
     A                                      CF12
     A                                      CHANGE(40)
     A N81N80                               CF04
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      INVITE
     A                                      OVERLAY
     A                                      HLPCLR
     A          H                           HLPPNLGRP(OMH02004 OMH020)
     A                                      HLPARA(01 002 01 002)
     A          H                           HLPPNLGRP('OMH02004/FMSTRC' OMH020)
     A                                      HLPARA(08 028 08 037)
     A          H                           HLPPNLGRP('OMH02004/FMSTRD' OMH020)
     A                                      HLPARA(09 028 09 077)
     A          H                           HLPPNLGRP('OMH02004/KEYS' OMH020)
     A                                      HLPARA(23 001 23 080)
     A            FMFMTC        10A  H
     A            FMFLDC        10A  H
     A                                  1  2'OMH020  '
     A            MH02000       50A  O  1 16TEXT('Question List Maintenance')
     A                                      MSGID(MH0 2000 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH02002'
     A            MH02003       50A  O  2 16TEXT('Maintain Question List')
     A                                      MSGID(MH0 2003 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A N80N81     ACT0001       10A  O  4  2MSGID(ACT 0001 OMHDSP)
     A                                      DSPATR(RI)
     A  80 81     ACT0003       10A  O  4  2MSGID(ACT 0003 OMHDSP)
     A                                      DSPATR(RI)
     A            APP0001       25A  O  5  2TEXT('HelpDesk Code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        O  5 28REFFLD(HEDC)
     A                                      TEXT('Application code')
     A                                      DSPATR(HI)
     A            FMHEDD    R   40   O  5 40REFFLD(HEDD)
     A                                      DSPATR(HI)
     A            EMP001        25A  O  6  2TEXT('Employee code')
     A                                      MSGID(EMP 0001 OMHDSP)
     A            FMEMPC    R        O  6 28REFFLD(USID)
     A                                      TEXT('Employee code')
     A                                      DSPATR(HI)
     A            FMUSDC    R   40   O  6 40REFFLD(USDC)
     A                                      TEXT('HelpDesk Description')
     A                                      DSPATR(HI)
     A            ROU0002       25A  O  8  2MSGID(ROU 0002 OMHDSP)
     A            FMSTRC    R        B  8 28REFFLD(STRC)
     A  80                                  DSPATR(HI)
     A  70                                  DSPATR(RI)
     A N80                                  DSPATR(UL)
     A  70                                  DSPATR(PC)
     A  80                                  DSPATR(PR)
     A  80        DSC0004       25A  O  9  2TEXT('Description')
     A                                      MSGID(DSC 0004 OMHDSP)
     A N41 80     FMSTRD    R        O  9 28REFFLD(STAD)
     A                                      DSPATR(HI)
     A  41 80     MH2006        50A  O  9 28MSGID(MH0 2006 OMHDSP)
     A                                      DSPATR(HI)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            KEY0003       78A  O 23  2TEXT('F3=Exit  F12=Cancel')
     A                                      MSGID(KEY 0003 OMHDSP)
     A                                      COLOR(BLU)
     A    N80     MH2007        25   O  8 40MSGID(MH0 2007 OMHDSP)
     A          R OMH02005
     A*%%TS  SD  19970709  140304  JAN         REL-V3R1M0  5763-PW1
     A                                      TEXT('Work with Routes')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A                                      KEEP
     A                                      OVERLAY
     A                                      HLPCLR
     A          H                           HLPPNLGRP(OMH02002 OMH020)
     A                                      HLPARA(01 002 01 002)
     A          H                           HLPPNLGRP('OMH02002/FMHEDC' OMH020)
     A                                      HLPARA(05 028 05 032)
     A          H                           HLPPNLGRP('OMH02002/FMPOS' OMH020)
     A                                      HLPARA(07 028 07 037)
     A          H                           HLPPNLGRP('OMH02001/FMOPTI' OMH020)
     A                                      HLPARA(12 002 20 003)
     A          H                           HLPPNLGRP('OMH02002/KEYS' OMH020)
     A                                      HLPARA(23 001 23 080)
     A            MH02000       50A  O  1 16TEXT('Question list maintenance')
     A                                      MSGID(MH0 2000 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH02001'
     A            MH02001       50A  O  2 16TEXT('Work with question lists')
     A                                      MSGID(MH0 2001 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            HED0001       25A  O  5  2TEXT('HelpDesk code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        O  5 28REFFLD(HEDC)
     A                                      TEXT('HelpDesk code')
     A                                      DSPATR(HI)
     A            FMHEDD    R   40   O  5 40REFFLD(HEDD)
     A                                      TEXT('HelpDesk Description')
     A                                      DSPATR(HI)
     A            POS0001       25A  O  8  2TEXT('Position at')
     A                                      MSGID(POS 0001 OMHDSP)
     A            FMPOS     R        B  8 28REFFLD(STRC)
     A                                      DSPATR(UL)
     A            MH02004       70A  O 10  2TEXT('Options')
     A                                      MSGID(MH0 2004 OMHDSP)
     A                                      COLOR(BLU)
     A            MH02002       68A  O 12  2TEXT('Subfile header')
     A                                      MSGID(MH0 2002 OMHDSP)
     A                                      DSPATR(HI)
     A            EMP001        25A  O  6  2TEXT('Employee code')
     A                                      MSGID(EMP 0001 OMHDSP)
     A            FMEMPC    R        O  6 28REFFLD(USID)
     A                                      TEXT('Employee code')
     A                                      DSPATR(HI)
     A            FMUSDC    R   40   O  6 40REFFLD(USDC)
     A                                      TEXT('HelpDesk Description')
     A                                      DSPATR(HI)
     A            MH02005       25   O 14  4TEXT('Position at')
     A                                      MSGID(MH0 2005 OMHDSP)
     A          R MSGSFL
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A          R MSGCTL
     A                                      SFLCTL(MSGSFL)
     A                                      SFLINZ
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N91                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
