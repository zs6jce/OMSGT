      *$$\CRTOM CRTDSPF
      *$$\ENDOM
     A*%%TS  SD  19980306  161027  WIM         REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/OMSREFHD)
     A                                      CHGINPDFT
     A                                      HLPTITLE('Remain Software')
     A                                      HLPSCHIDX(*LIBL/REMAINHD)
     A                                      HELP
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      ALTHELP(CA01)
     A          R OMH03801                  SFL
     A*%%TS  SD  19980306  161027  WIM         REL-V3R1M0  5763-PW1
     A                                      TEXT('Work with Distributions (subf-
     A                                      ile)')
     A  93                                  SFLNXTCHG
     A            FMDSID        20A  H
     A            FMDSQ#         4A  H
     A            FMUSRD    R        H      REFFLD(USRI)
     A            FMADDR    R        H      REFFLD(ADDR)
     A            FMOPTI         2A  B 10  2TEXT('Option field')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A            F2SUBJ        29A  O 10  6
     A            FMHEDC    R        O 10 36REFFLD(HEDC)
     A            FMINC#    R        O 10 42REFFLD(INC#)
     A            FMCUSC    R        O 10 53REFFLD(CUSC)
     A            FMCCTC    R        O 10 64REFFLD(CCTC)
     A            FMSTAT    R        O 10 75REFFLD(STAT)
     A          R OMH03802                  SFLCTL(OMH03801)
     A*%%TS  SD  19970908  162207  WIM         REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      TEXT('Work with Distributions')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A  92                                  ROLLUP
     A                                      CF21
     A                                      CF04
     A                                      KEEP
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      OVERLAY
     A                                      ERASE(OMH03801)
     A  90                                  SFLDSP
     A                                      SFLDSPCTL
     A  91                                  SFLCLR
     A N92                                  SFLEND(*MORE)
     A                                      HLPCLR
     A          H                           HLPARA(01 002 01 002)
     A                                      HLPPNLGRP('OMH03802' OMH038)
     A          H                           HLPARA(10 002 20 003)
     A                                      HLPPNLGRP('OMH03801/FMOPTI' OMH038)
     A          H                           HLPARA(23 001 23 080)
     A                                      HLPPNLGRP('OMH03802/KEYS' OMH038)
     A            FMFLDC        10A  H
     A            FMFMTC        10A  H
     A            CRRN01         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'OMH038  '
     A            MH03800       50A  O  1 16TEXT('Question list maintenance')
     A                                      MSGID(MH0 3800 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH03801'
     A            MH03801       50A  O  2 16TEXT('Work with question lists')
     A                                      MSGID(MH0 3801 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            MH03804       78A  O  7  2TEXT('Options')
     A                                      MSGID(MH0 3804 OMHDSP)
     A                                      COLOR(BLU)
     A            MH03802       78A  O  9  2TEXT('Subfile header')
     A                                      MSGID(MH0 3802 OMHDSP)
     A                                      DSPATR(HI)
     A          R OMH03803
     A*%%TS  SD  19970904  121220  WIM         REL-V3R1M0  5763-PW1
     A                                      TEXT('Work with Distributions (foot-
     A                                      er)')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A                                      ROLLUP(92)
     A                                      OVERLAY
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            MH03805       78   O 23  2MSGID(MH0 3805 OMHDSP)
     A                                      COLOR(BLU)
     A          R OMH03804
     A*%%TS  SD  19970909  170756  WIM         REL-V3R1M0  5763-PW1
     A                                      TEXT('Maintain Distribution')
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF12
     A                                      CHANGE(40)
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      OVERLAY
     A                                      HLPCLR
     A          H                           HLPARA(01 002 01 002)
     A                                      HLPPNLGRP('OMH03804' OMH038)
     A          H                           HLPARA(09 028 09 032)
     A                                      HLPPNLGRP('OMH03804/FMHEDC' OMH038)
     A          H                           HLPARA(10 028 10 037)
     A                                      HLPPNLGRP('OMH03804/FMINCY' OMH038)
     A          H                           HLPARA(11 028 11 077)
     A                                      HLPPNLGRP('OMH03804/FMSHID' OMH038)
     A          H                           HLPARA(13 028 13 037)
     A                                      HLPPNLGRP('OMH03804/FMCUSC' OMH038)
     A          H                           HLPARA(14 028 14 037)
     A                                      HLPPNLGRP('OMH03804/FMCCTC' OMH038)
     A          H                           HLPARA(16 028 16 037)
     A                                      HLPPNLGRP('OMH03804/FMRUID' OMH038)
     A          H                           HLPARA(18 028 18 037)
     A                                      HLPPNLGRP('OMH03804/FMSTRC' OMH038)
     A          H                           HLPARA(19 028 19 030)
     A                                      HLPPNLGRP('OMH03804/FMRESY' OMH038)
     A          H                           HLPARA(21 028 21 028)
     A                                      HLPPNLGRP('OMH03804/FMCSRC' OMH038)
     A          H                           HLPARA(23 001 23 080)
     A                                      HLPPNLGRP('OMH03804/KEYS' OMH038)
     A            FMFMTC        10A  H
     A            FMFLDC        10A  H
     A                                  1  2'OMH038  '
     A            MH03800       50A  O  1 16MSGID(MH0 3800 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH03802'
     A            MH00903       50A  O  2 16MSGID(MH0 3803 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A  80N81     ACT0002       10A  O  4  2MSGID(ACT 0002 OMHDSP)
     A                                      DSPATR(RI)
     A  80 81     ACT0003       10A  O  4  2MSGID(ACT 0003 OMHDSP)
     A                                      DSPATR(RI)
     A            DST0010       25A  O  5  2TEXT('Distribution ID')
     A                                      MSGID(DST 0010 OMHDSP)
     A            FMDSID        20A  O  5 28DSPATR(HI)
     A            STA0001       25A  O  6  2TEXT('Status')
     A                                      MSGID(STA 0001 OMHDSP)
     A            FMSTAT    R        O  6 28REFFLD(STAT)
     A                                      DSPATR(HI)
     A            SUB0001       25A  O  7  2TEXT('Subject')
     A                                      MSGID(SUB 0001 OMHDSP)
     A            FMSUBJ        50A  O  7 28DSPATR(HI)
     A            HED0001       25A  O  9  2TEXT('Helpdesk')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC         5A  B  9 28DSPATR(UL)
     A            KEYF402       30A  O  9 42MSGID(KEY F402 OMHDSP)
     A            INC0006       25A  O 10  2TEXT('Incident')
     A                                      MSGID(INC 0006 OMHDSP)
     A            FMINC#        10A  B 10 28DSPATR(UL)
     A            DSC0004       25   O 11  2TEXT('Description')
     A                                      MSGID(DSC 0004 OMHDSP)
     A            FMSHID    R        B 11 28REFFLD(SHID)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A            CUS0003       25A  O 13  2TEXT('Customer')
     A                                      MSGID(CUS 0003 OMHDSP)
     A            FMCUSC        10A  B 13 28DSPATR(UL)
     A            KEYF4002      30A  O 13 42MSGID(KEY F402 OMHDSP)
     A            CUS0004       25A  O 14  2TEXT('Contact')
     A                                      MSGID(CUS 0004 OMHDSP)
     A            FMCCTC        10A  B 14 28DSPATR(UL)
     A            KEYF4003      30A  O 14 42MSGID(KEY F402 OMHDSP)
     A            EMP0001       25A  O 16  2TEXT('Employee')
     A                                      MSGID(EMP 0001 OMHDSP)
     A            FMRUID        10A  B 16 28DSPATR(UL)
     A            KEYF4004      30A  O 16 42MSGID(KEY F402 OMHDSP)
     A            STA0004       25   O 18  2TEXT('Status route')
     A                                      MSGID(STA 0004 OMHDSP)
     A            FMSTRC    R     A  B 18 28REFFLD(STRC)
     A                                      DSPATR(UL)
     A            FLD003        30A  O 18 42MSGID(KEY F402 OMHDSP)
     A            SEQ0001       25   O 19  2TEXT('Sequence')
     A                                      MSGID(SEQ 0001 OMHDSP)
     A            FMRES#    R        B 19 28REFFLD(RES#)
     A                                      DSPATR(UL)
     A                                      EDTCDE(3)
     A            FLD007        30A  O 19 42MSGID(KEY F402 OMHDSP)
     A            CSR0001       25A  O 21  2TEXT('Call source code')
     A                                      MSGID(CSR 0001 OMHDSP)
     A            FMCSRC         1A  B 21 28DSPATR(UL)
     A            CHO0064       45A  O 21 35MSGID(CHO 0064 OMHDSP)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            MH03805       78A  O 23  2MSGID(MH0 3805 OMHDSP)
     A                                      COLOR(BLU)
     A          R OMH03805                  SFL
     A*%%TS  SD  19970904  163851  WIM         REL-V3R1M0  5763-PW1
     A                                      TEXT('Work with Distributions (subf-
     A                                      ile)')
     A  93                                  SFLNXTCHG
     A            FMINCD    R        O 10  2REFFLD(INCD)
     A          R OMH03806                  SFLCTL(OMH03805)
     A*%%TS  SD  19970908  161445  WIM         REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      TEXT('Work with Distributions')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A  92                                  ROLLUP
     A                                      CF21
     A                                      CF04
     A                                      KEEP
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      OVERLAY
     A                                      ERASE(OMH03801)
     A  90                                  SFLDSP
     A                                      SFLDSPCTL
     A  91                                  SFLCLR
     A N92                                  SFLEND(*MORE)
     A                                      HLPCLR
     A          H                           HLPARA(01 002 01 002)
     A                                      HLPPNLGRP('OMH03806' OMH038)
     A          H                           HLPARA(23 001 23 080)
     A                                      HLPPNLGRP('OMH03806/KEYS' OMH038)
     A            FMFLDC        10A  H
     A            FMFMTC        10A  H
     A            CRRN03         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'OMH038  '
     A            MH03800       50A  O  1 16TEXT('Question list maintenance')
     A                                      MSGID(MH0 3800 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH03803'
     A            MH03806       50A  O  2 16MSGID(MH0 3806 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            DST0010       25A  O  5  2TEXT('Distribution ID')
     A                                      MSGID(DST 0010 OMHDSP)
     A            FMDSID        20A  O  5 28DSPATR(HI)
     A            MH03807       76   O  9  2TEXT('Subfile header')
     A                                      MSGID(MH0 3807 OMHDSP)
     A                                      DSPATR(HI)
     A          R OMH03807
     A*%%TS  SD  19970904  121220  WIM         REL-V3R1M0  5763-PW1
     A                                      TEXT('Work with Distributions (foot-
     A                                      er)')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A                                      ROLLUP(92)
     A                                      OVERLAY
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            MH03805       78   O 23  2MSGID(MH0 3805 OMHDSP)
     A                                      COLOR(BLU)
     A          R MSGSFL                    SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A          R MSGCTL
     A                                      SFLCTL(MSGSFL)
     A                                      SFLINZ
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N91                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
     A          R OMH03814
     A*%%TS  SD  19970904  163851  WIM         REL-V3R1M0  5763-PW1
     A                                      TEXT('Maintain Distribution')
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF12
     A                                      CHANGE(40)
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      OVERLAY
     A                                      HLPCLR
     A          H                           HLPARA(01 002 01 002)
     A                                      HLPPNLGRP('OMH03814' OMH038)
     A          H                           HLPARA(09 028 09 032)
     A                                      HLPPNLGRP('OMH03814/FMHEDC' OMH038)
     A          H                           HLPARA(10 028 10 037)
     A                                      HLPPNLGRP('OMH03814/FMINCY' OMH038)
     A          H                           HLPARA(12 028 12 037)
     A                                      HLPPNLGRP('OMH03814/FMCUSC' OMH038)
     A          H                           HLPARA(13 028 13 037)
     A                                      HLPPNLGRP('OMH03814/FMCCTC' OMH038)
     A          H                           HLPARA(15 028 15 037)
     A                                      HLPPNLGRP('OMH03814/FMRUID' OMH038)
     A          H                           HLPARA(17 028 17 028)
     A                                      HLPPNLGRP('OMH03814/FMCSRC' OMH038)
     A          H                           HLPARA(23 001 23 080)
     A                                      HLPPNLGRP('OMH03814/KEYS' OMH038)
     A            FMFMTC        10A  H
     A            FMFLDC        10A  H
     A                                  1  2'OMH038  '
     A            MH03800       50A  O  1 16MSGID(MH0 3800 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH03802'
     A            MH00903       50A  O  2 16MSGID(MH0 3803 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A  80N81     ACT0002       10A  O  4  2MSGID(ACT 0002 OMHDSP)
     A                                      DSPATR(RI)
     A  80 81     ACT0003       10A  O  4  2MSGID(ACT 0003 OMHDSP)
     A                                      DSPATR(RI)
     A            STA0001       25A  O  6  2TEXT('Status')
     A                                      MSGID(STA 0001 OMHDSP)
     A            FMSTAT    R        O  6 28REFFLD(STAT)
     A                                      DSPATR(HI)
     A            SUB0001       25A  O  7  2TEXT('Subject')
     A                                      MSGID(SUB 0001 OMHDSP)
     A            FMSUBJ        50A  O  7 28DSPATR(HI)
     A            HED0001       25A  O  9  2TEXT('Helpdesk')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC         5A  B  9 28DSPATR(UL)
     A            KEYF402       30A  O  9 42MSGID(KEY F402 OMHDSP)
     A            INC0006       25A  O 10  2TEXT('Incident')
     A                                      MSGID(INC 0006 OMHDSP)
     A            FMINC#        10A  B 10 28DSPATR(UL)
     A            CUS0003       25A  O 12  2TEXT('Customer')
     A                                      MSGID(CUS 0003 OMHDSP)
     A            FMCUSC        10A  B 12 28DSPATR(UL)
     A            KEYF4002      30A  O 12 42MSGID(KEY F402 OMHDSP)
     A            CUS0004       25A  O 13  2TEXT('Contact')
     A                                      MSGID(CUS 0004 OMHDSP)
     A            FMCCTC        10A  B 13 28DSPATR(UL)
     A            KEYF4003      30A  O 13 42MSGID(KEY F402 OMHDSP)
     A            EMP0001       25A  O 15  2TEXT('Employee')
     A                                      MSGID(EMP 0001 OMHDSP)
     A            FMRUID        10A  B 15 28DSPATR(UL)
     A            KEYF4004      30A  O 15 42MSGID(KEY F402 OMHDSP)
     A            CSR0001       25A  O 17  2TEXT('Call source code')
     A                                      MSGID(CSR 0001 OMHDSP)
     A            FMCSRC         1A  B 17 28DSPATR(UL)
     A            CHO0064       45A  O 17 35MSGID(CHO 0064 OMHDSP)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            MH03805       78A  O 23  2MSGID(MH0 3805 OMHDSP)
     A                                      COLOR(BLU)
