      *$$\CRTOM CRTDSPF
      *$$\ENDOM
     A                                      HLPTITLE('Remain Software')
     A                                      HLPSCHIDX(REMAINHD)
     A                                      HELP
     A*%%TS  SD  19970219  155716  WIM         REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/OMSREFHD)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      ALTHELP(CA01)
     A          R OMH00601                  SFL
     A*%%TS  SD  19970108  165859  EWOUT       REL-V3R1M0  5763-PW1
     A                                      TEXT('Work with Answers (subfile)')
     A  93                                  SFLNXTCHG
     A            FMOPTI         1A  B 13  3TEXT('Option field')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A            FMANS#    R        O 13 10REFFLD(ANS#)
     A                                      EDTCDE(3)
     A            FMANS1    R   65   O 13 15REFFLD(ANSC)
     A                                      TEXT('Answer')
     A          R OMH00602                  SFLCTL(OMH00601)
     A*%%TS  SD  19970108  165313  EWOUT       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      TEXT('Work with Answers')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A  92                                  ROLLUP
     A                                      KEEP
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      OVERLAY
     A                                      ERASE(OMH00601)
     A  90                                  SFLDSP
     A                                      SFLDSPCTL
     A  91                                  SFLCLR
     A N92                                  SFLEND(*MORE)
     A                                      HLPCLR
     A          H                           HLPARA(01 002 01 002)
     A                                      HLPPNLGRP('OMH00602' OMH006)
     A          H                           HLPARA(08 028 08 030)
     A                                      HLPPNLGRP('OMH00602/FMPOS' OMH006)
     A          H                           HLPARA(13 003 20 003)
     A                                      HLPPNLGRP('OMH00601/FMOPTI' OMH006)
     A          H                           HLPARA(23 001 23 080)
     A                                      HLPPNLGRP('OMH00602/KEYS' OMH006)
     A            FMFLDC        10A  H
     A            FMFMTC        10A  H
     A            CRRN01         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'OMH006  '
     A            MH00600       50A  O  1 16TEXT('Question list maintenance')
     A                                      MSGID(MH0 0600 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH00601'
     A            MH00601       50A  O  2 16TEXT('Work with question lists')
     A                                      MSGID(MH0 0601 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            HED0001       25A  O  5  2TEXT('HelpDesk code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        O  5 28REFFLD(HEDC)
     A                                      TEXT('HelpDesk code')
     A                                      DSPATR(HI)
     A            FMHEDD    R   40   O  5 34REFFLD(HEDD)
     A                                      TEXT('HelpDesk Description')
     A                                      DSPATR(HI)
     A            QUE0001       25   O  6  2TEXT('Question number')
     A                                      MSGID(QUE 0002 OMHDSP)
     A            FMQUE#    R     Y  O  6 28REFFLD(QUE#)
     A                                      EDTCDE(3)
     A                                      DSPATR(HI)
     A            FMQUE1    R   45   O  6 34REFFLD(QUES)
     A                                      DSPATR(HI)
     A            POS0001       25A  O  8  2TEXT('Position at')
     A                                      MSGID(POS 0001 OMHDSP)
     A            FMPOS     R     Y  B  8 28REFFLD(ANS#)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A            OPT0001       70A  O 10  2TEXT('Options')
     A                                      MSGID(OPT 0001 OMHDSP)
     A                                      COLOR(BLU)
     A            MH00602       78A  O 12  2TEXT('Subfile header')
     A                                      MSGID(MH0 0602 OMHDSP)
     A                                      DSPATR(HI)
     A          R OMH00603
     A*%%TS  SD  19960909  172708  WIM         REL-V3R1M0  5763-PW1
     A                                      TEXT('Work with Answers (footer)')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A                                      ROLLUP(92)
     A                                      OVERLAY
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            MH00604       78   O 23  2MSGID(MH0 0604 OMHDSP)
     A                                      COLOR(BLU)
     A          R OMH00604
     A*%%TS  SD  19960909  143143  WIM         REL-V3R1M0  5763-PW1
     A                                      TEXT('Maintain Answer')
     A                                      CF03
     A                                      CF12
     A                                      CHANGE(40)
     A N80
     AON81                                  CF04
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      INVITE
     A                                      OVERLAY
     A                                      HLPCLR
     A          H                           HLPARA(01 002 01 002)
     A                                      HLPPNLGRP('OMH00604' OMH006)
     A          H                           HLPARA(07 028 07 030)
     A                                      HLPPNLGRP('OMH00604/FMANSY' OMH006)
     A          H                           HLPARA(08 028 08 080)
     A                                      HLPPNLGRP('OMH00604/FMANSC' OMH006)
     A          H                           HLPARA(23 001 23 080)
     A                                      HLPPNLGRP('OMH00604/KEYS' OMH006)
     A            FMFMTC        10A  H
     A            FMFLDC        10A  H
     A                                  1  2'OMH006  '
     A            MH00600       50A  O  1 16TEXT('Question List Maintenance')
     A                                      MSGID(MH0 0600 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH00602'
     A            MH00603       50A  O  2 16TEXT('Maintain Question List')
     A                                      MSGID(MH0 0603 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A N80N81     ACT0001       10A  O  4  2MSGID(ACT 0001 OMHDSP)
     A                                      DSPATR(RI)
     A  80N81     ACT0002       10A  O  4  2MSGID(ACT 0002 OMHDSP)
     A                                      DSPATR(RI)
     A  80 81     ACT0003       10A  O  4  2MSGID(ACT 0003 OMHDSP)
     A                                      DSPATR(RI)
     A            APP0001       25A  O  5  2TEXT('HelpDesk Code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        O  5 28REFFLD(HEDC)
     A                                      TEXT('Application code')
     A                                      DSPATR(HI)
     A            FMHEDD    R   40   O  5 34REFFLD(HEDD)
     A                                      DSPATR(HI)
     A  80
     AON80        ANS0002       25   O  7  2TEXT('Answer')
     A                                      MSGID(ANS 0002 OMHDSP)
     A            FMANS#    R        B  7 28REFFLD(ANS#)
     A  80                                  DSPATR(HI)
     A  70N80                               DSPATR(RI)
     A N80                                  DSPATR(UL)
     A  70N80                               DSPATR(PC)
     A  80                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A            ANS0001       25   O  8  2TEXT('Answer')
     A                                      MSGID(ANS 0001 OMHDSP)
     A            FMANSC    R        B  8 28REFFLD(ANSC)
     A  81                                  DSPATR(HI)
     A  71N81                               DSPATR(RI)
     A N81                                  DSPATR(UL)
     A  71N81                               DSPATR(PC)
     A  81                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      TEXT('Question')
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            KEY0003       78A  O 23  2TEXT('F3=Exit  F12=Cancel')
     A                                      MSGID(KEY 0003 OMHDSP)
     A                                      COLOR(BLU)
     A          R MSGSFL                    SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A          R MSGCTL
     A                                      SFLCTL(MSGSFL)
     A                                      SFLINZ
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N91                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
