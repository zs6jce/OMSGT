      *$$\CRTOM CRTDSPF
      *$$\ENDOM
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/OMSREFHD)
     A                                      CHGINPDFT
     A                                      HLPTITLE('Remain Software')
     A                                      HLPSCHIDX(*LIBL/REMAINHD)
     A                                      HELP
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      ALTHELP(CA01)
      *
     A          R OMH00201                  SFL
     A                                      TEXT('Work with Employee (subfile)')
     A  93                                  SFLNXTCHG
     A            FMOPTI         2A  B 12  3TEXT('Option field')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A            FMUSID    R        O 12  7REFFLD(USID)
     A                                      TEXT('User id or user class')
     A            FMSUL#    R        O 12 25REFFLD(SUL#)
     A            FMUSDC    R   48A  O 12 32REFFLD(USDC)
     A                                      TEXT('User description')
      *
     A          R OMH00202                  SFLCTL(OMH00201)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      TEXT('Work with Employee')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A  92                                  ROLLUP
     A                                      CF04
     A                                      KEEP
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      OVERLAY
     A                                      ERASE(OMH00201)
     A  90                                  SFLDSP
     A                                      SFLDSPCTL
     A  91                                  SFLCLR
     A N92                                  SFLEND(*MORE)
     A                                      HLPCLR
     A          H                           HLPPNLGRP(OMH00202 OMH002)
     A                                      HLPARA(01 002 01 002)
     A          H                           HLPPNLGRP('OMH00202/FMHEDC' OMH002)
     A                                      HLPARA(05 028 05 032)
     A          H                           HLPPNLGRP('OMH00202/FMPOS' OMH002)
     A                                      HLPARA(07 028 07 037)
     A          H                           HLPPNLGRP('OMH00201/FMOPTI' OMH002)
     A                                      HLPARA(12 003 20 003)
     A          H                           HLPPNLGRP('OMH00202/KEYS' OMH002)
     A                                      HLPARA(23 001 23 080)
     A            FMFLDC        10A  H
     A            FMFMTC        10A  H
     A            CRRN01         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'OMH002  '
     A            MH00200       50A  O  1 17TEXT('Security maintenance')
     A                                      MSGID(MH0 0200 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH00201'
     A            MH00201       50A  O  2 17TEXT('Work with user rights')
     A                                      MSGID(MH0 0201 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            HED0001       25A  O  5  2TEXT('HelpDesk code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        B  5 28REFFLD(HEDC)
     A                                      TEXT('HelpDesk code')
     A  71                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  71                                  DSPATR(PC)
     A            FMHEDD    R   40   O  5 35REFFLD(HEDD)
     A                                      TEXT('HelpDesk Description')
     A                                      DSPATR(HI)
     A            POS0001       25A  O  7  2TEXT('Position at')
     A                                      MSGID(POS 0001 OMHDSP)
     A            FMPOS     R        B  7 28REFFLD(USID)
     A                                      TEXT('Position at')
     A                                      DSPATR(UL)
     A  72                                  DSPATR(PC)
     A            OPT0071       70   O  9  2MSGID(OPT 0071 OMHDSP)
     A                                      COLOR(BLU)
     A            MH00202       78A  O 11  2MSGID(MH0 0202 OMHDSP)
     A                                      DSPATR(HI)
      *
     A          R OMH00203
     A                                      TEXT('Work with Employee (footer)')
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF12
     A                                      ROLLUP(92)
     A                                      OVERLAY
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            KEY0064       78   O 23  2TEXT('F3/F5/F6/F12 F21=Print list')
     A                                      MSGID(KEY 0064 OMHDSP)
     A                                      COLOR(BLU)
      *
     A          R OMH00204
     A                                      TEXT('Maintain Employee')
     A                                      CF03
     A                                      CF12
     A                                      CHANGE(40)
     A N80
     AON81                                  CF04
     A                                      RTNCSRLOC(&FMFMTC &FMFLDC)
     A                                      INVITE
     A                                      OVERLAY
     A                                      HLPCLR
     A          H                           HLPPNLGRP(OMH00204 OMH002)
     A                                      HLPARA(01 002 01 002)
     A          H                           HLPPNLGRP('OMH00204/FMUSID' OMH002)
     A                                      HLPARA(07 028 07 037)
     A          H                           HLPPNLGRP('OMH00204/FMSULY' OMH002)
     A                                      HLPARA(08 028 08 028)
     A          H                           HLPPNLGRP('OMH00204/FMEMAC' OMH002)
     A                                      HLPARA(09 028 09 077)
     A          H                           HLPPNLGRP('OMH00204/FMALOC' OMH002)
     A                                      HLPARA(10 028 10 028)
     A          H                           HLPPNLGRP('OMH00204/KEYS' OMH002)
     A                                      HLPARA(23 001 23 080)
     A            FMFMTC        10A  H
     A            FMFLDC        10A  H
     A                                  1  2'OMH002  '
     A            MH00200       50A  O  1 17TEXT('Employee Maintenance')
     A                                      MSGID(MH0 0200 OMHDSP)
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'OMH00202'
     A            MH00203       50A  O  2 17TEXT('Maintain Employee')
     A                                      MSGID(MH0 0203 OMHDSP)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A N80N81     ACT0001       10A  O  4  2MSGID(ACT 0001 OMHDSP)
     A                                      DSPATR(RI)
     A  80N81     ACT0002       10A  O  4  2MSGID(ACT 0002 OMHDSP)
     A                                      DSPATR(RI)
     A  80 81     ACT0003       10A  O  4  2MSGID(ACT 0003 OMHDSP)
     A                                      DSPATR(RI)
     A            APP0001       25A  O  5  2TEXT('HelpDesk Code')
     A                                      MSGID(HED 0001 OMHDSP)
     A            FMHEDC    R        O  5 28REFFLD(HEDC)
     A                                      TEXT('Application code')
     A                                      DSPATR(HI)
     A            FMHEDD    R   40   O  5 35REFFLD(HEDD)
     A                                      TEXT('Application name')
     A                                      DSPATR(HI)
     A            USR0001       25A  O  7  2TEXT('Employee')
     A                                      MSGID(EMP 0001 OMHDSP)
     A            FMUSID    R        B  7 28REFFLD(USID)
     A                                      TEXT('Employee')
     A  80                                  DSPATR(HI)
     A  70N80                               DSPATR(RI)
     A N80                                  DSPATR(UL)
     A  70N80                               DSPATR(PC)
     A  80                                  DSPATR(PR)
     A N80        PMT0001       25A  O  7 41TEXT('F4=Prompt')
     A                                      MSGID(PMT 0001 OMHDSP)
     A            SUP0002       25   O  8  2TEXT('Support level')
     A                                      MSGID(SUP 0002 OMHDSP)
     A            FMSUL#    R        B  8 28REFFLD(SUL#)
     A  81                                  DSPATR(HI)
     A  71N81                               DSPATR(RI)
     A N81                                  DSPATR(UL)
     A  71N81                               DSPATR(PC)
     A  81                                  DSPATR(PR)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            KEY0003       78A  O 23  2TEXT('F3=Exit  F12=Cancel')
     A                                      MSGID(KEY 0003 OMHDSP)
     A                                      COLOR(BLU)
     A            EMA001        25   O  9  2MSGID(EMA 0001 OMHDSP)
     A            FMEMAC        50A  B  9 28
     A  81                                  DSPATR(HI)
     A  71N81                               DSPATR(RI)
     A N81                                  DSPATR(UL)
     A  71N81                               DSPATR(PC)
     A  81                                  DSPATR(PR)
     A            ALC001        25   O 10  2MSGID(ALC 0002 OMHDSP)
     A            CHO0024       15   O 10 35MSGID(CHO 0024 OMHDSP)
     A            FMALOC         1A  B 10 28
     A  81                                  DSPATR(HI)
     A  71N81                               DSPATR(RI)
     A N81                                  DSPATR(UL)
     A  71N81                               DSPATR(PC)
     A  81                                  DSPATR(PR)
     A          R MSGSFL                    SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A
     A          R MSGCTL
     A                                      SFLCTL(MSGSFL)
     A                                      SFLINZ
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N91                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
